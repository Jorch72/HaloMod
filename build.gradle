buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
			url = 'https://oss.sonatype.org/content/groups/public' 
		}
    }
    dependencies {
		classpath 'com.matthewprenger:CurseGradle:1.0-SNAPSHOT'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

version = "v1.6.${System.env.TRAVIS_BUILD_NUMBER}"
group = "io.github.hsyyid"
archivesBaseName = "halocraft-2.0-DEV"

tasks.withType(JavaCompile) {
	sourceCompatibility = "1.8"
	targetCompatibility = "1.8"
	options.encoding = 'UTF-8'
}

minecraft {
    version = "1.10.2-12.18.1.2046"
    runDir = "run"
    mappings = "snapshot_20160808"
}

def curseForgeApiKey = hasProperty('curseForgeApiKey') ? curseForgeApiKey : System.getenv('curseForgeApiKey')

curseforge {
    apiKey = curseForgeApiKey
	project {
		id = '232011'
		changelog = 'Look at the CHANGELOG.txt to see changes.'
		releaseType = 'beta'
		addGameVersion '1.10.2'
		
		mainArtifact(jar) {
			displayName = "${project.archivesBaseName} ${project.version}"
		}
	}
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade files("libs/AIRIX.jar")
}

jar {
    includeEmptyDirs = false
    classifier = 'universal'
    version = "${project.minecraft.version}-${project.version}"
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
            exclude 'mcmod.info'
        }
    }
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
